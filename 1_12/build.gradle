import io.github.headlesshq.headlessmc.gradle.HeadlessMcRunTask
import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import me.earth.headlessmc.launcher.modlauncher.Modlauncher

buildscript {
    repositories {
        maven {
            name = "3arthMaven"
            url = "https://3arthqu4ke.github.io/maven"
        }
    }

    dependencies {
        classpath "me.earth.headlessmc:headlessmc-launcher:2.1.0"
        classpath "io.github.headlesshq:headlessmc-gradle-plugin:0.3.0"
        // not 100% sure, the headlessmc-launcher maven metadata exposes this with runtime scope? But still needed
        // Unable to load class 'org.semver4j.Semver'.
        // TODO: file report?
        // This is an unexpected error. Please file a bug containing the idea.log file.
        classpath "org.semver4j:semver4j:5.3.0"
    }
}

plugins {
    id 'java'
    id 'xyz.wagyourtail.unimined' version '1.3.10-SNAPSHOT'
    id 'com.gradleup.shadow' version '8.3.0'
    id 'io.github.headlesshq.headlessmc-gradle-plugin' version '0.3.0'
}

group 'me.earth.headlessmc'
version '0.4.0'

base {
    archivesName = "hmc-optimizations-${minecraft_version}"
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

sourceSets {
    fabric
    lexforge
}

sourceSets {
    fabric {
        compileClasspath += sourceSets.main.output + sourceSets.main.compileClasspath
        runtimeClasspath += sourceSets.main.output + sourceSets.main.compileClasspath
    }
    lexforge {
        compileClasspath += sourceSets.main.output + sourceSets.main.compileClasspath
        runtimeClasspath += sourceSets.main.output + sourceSets.main.compileClasspath
    }
}

repositories {
    mavenCentral()
    maven {
        url = "https://files.minecraftforge.net/maven"
    }
    maven {
        name = 'impactdevelopment-repo'
        url = 'https://impactdevelopment.github.io/maven/'
    }
    maven {
        name = "sponge"
        url = "https://repo.spongepowered.org/maven"
    }
    maven {
        url = "https://maven.wagyourtail.xyz/releases"
    }

    maven {
        name = '3arthMaven'
        url = 'https://3arthqu4ke.github.io/maven'
    }
}

unimined.minecraft {
    version project.minecraft_version

    mappings {
        searge()
        mcp("stable", "39-1.12")
    }

    runs {
        // https://github.com/unimined/unimined/issues/98
        off = true
    }

    defaultRemapJar = false
}

unimined.minecraft(sourceSets.fabric) {
    combineWith(sourceSets.main)

    legacyFabric {
        loader project.fabric_version
    }

    defaultRemapJar = true
}

unimined.minecraft(sourceSets.lexforge) {
    combineWith(sourceSets.main)

    minecraftForge {
        loader project.lexforge_version
        mixinConfig 'hmcoptimizations.mixins.json'
    }

    minecraftRemapper.config {
        ignoreConflicts(true)
    }

    defaultRemapJar = true
}


configurations {
    mainImplementation
    implementation {
        extendsFrom lexforgeImplementation
        extendsFrom fabricImplementation
    }

    jarLibs
    implementation.extendsFrom jarLibs
    lwjglAgent.extendsFrom runtimeOnly
}

sourceSets {
    main {
        compileClasspath += configurations.mainImplementation
        runtimeClasspath += configurations.mainImplementation
    }
}

dependencies {
    implementation 'net.minecraft:launchwrapper:1.12'
    implementation 'org.spongepowered:mixin:0.8.5-SNAPSHOT'
    lexforgeImplementation 'org.spongepowered:mixin:0.8.5-SNAPSHOT'
    implementation 'me.earth.headlessmc:headlessmc-api:2.1.0'

    lwjglAgent 'me.earth.headlessmc:headlessmc-lwjgl:2.1.0'
    jarLibs 'com.github.ImpactDevelopment:SimpleTweaker:1.2'
    // TODO: package 0.8.5 instead?
    jarLibs('org.spongepowered:mixin:0.8.5-SNAPSHOT') {
        exclude module: 'launchwrapper'
        exclude module: 'guava'
        exclude module: 'gson'
        exclude module: 'commons-io'
    }
}


/* runs.off = true afterEvaluate {
    fabricRunClient {
        standardInput = System.in
        if (rootProject.property('hmc.lwjgl').toBoolean()) {
            jvmArgs += ["-javaagent:${configurations.lwjglAgent.files.iterator().next()}"]
            systemProperties['joml.nounsafe'] = 'true'
            systemProperties['fabric.systemLibraries'] = "${configurations.lwjglAgent.files.iterator().next()}"
        }
    }
}*/

jar {
    enabled = false
}

processFabricResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

processLexforgeResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }

    filesMatching("mcmod.info") {
        expand "version": project.version
    }
}

// Forge Runs seem to have problems running from the build/classes folder
// So instead we just run from the built jar
/* runs.off = true afterEvaluate {
    lexforgeRunClient {
        dependsOn(lexforgeJar)
        classpath = classpath.filter {
            !it.toString().contains('hmc-optimizations/build/classes/java/'.replace('/', File.separator))
                    && !it.toString().contains('hmc-optimizations/build/resources/'.replace('/', File.separator))
        }

        classpath += files("${projectDir}/build/libs/hmc-optimizations-${version}-lexforge-dev.jar".replace('/', File.separator))
    }
}*/

tasks.withType(org.gradle.jvm.tasks.Jar).configureEach {
    from("LICENSE") {
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
        rename { "${it}_${project.archivesBaseName}" }
    }

    exclude "**net/minecraftforge/**/Mod.class"

    manifest {
        attributes(
            'Implementation-Title': 'HMC-Optimizations',
            'TweakClass': 'me.earth.headlessmc.optimizations.tweaker.HmcOptimizationsTweaker',
            'MixinConfigs': "hmcoptimizations.mixins.json",
            'Implementation-Version': project.version,
        )
    }
}

for (Modlauncher mLauncher : Modlauncher.values()) {
    def ml = mLauncher
    if (ml != Modlauncher.NEOFORGE) {
        def platform = ml.hmcName
        def platform_capitalized = ml.hmcName.capitalize()
        def remapJarTask = tasks.named("remap${platform_capitalized}Jar", AbstractArchiveTask).get()
        remapJarTask.archiveClassifier.set("${platform}-remapped")
        def shadowTask = tasks.register("${platform}ShadowJar", ShadowJar) {
            dependsOn(remapJarTask)
            it.group = 'build'
            it.archiveClassifier = "${platform}"
            from remapJarTask.outputs
            it.configurations += [ project.configurations.jarLibs ]
            // Mixin is signed
            exclude 'META-INF/*.RSA'
            exclude 'META-INF/*.SF'
        }

        tasks.named('build') { finalizedBy(shadowTask) }

        tasks.register("${ml.officialName}RunWithHeadlessMc", HeadlessMcRunTask) {
            group 'headlessmc'
            from(tasks.named("${platform}ShadowJar"))
            into(modsDirectory)

            version = "${minecraft_version}"
            modlauncher = ml
            lwjgl = rootProject.property('hmc.lwjgl').toBoolean()
            dummyAssets = rootProject.property('hmc.dummy.assets').toBoolean()
            mcRuntimeTest = rootProject.property('mc.runtime.test').toBoolean()
            // specifics = true
        }
    }
}

remapFabricSourcesJar {
    enabled = false
}

remapLexforgeSourcesJar {
    enabled = false
}
